apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group 'com.spoqa.longpresslistener'
version '1.0.0'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'LongPressListener'
    organization = 'spoqa'

    // Library Details
    publishedGroupId = this.group
    libraryName = 'LongPressListener'
    artifact = 'LongPressListener'
    libraryDescription = 'Android long press(click) listener with custom duration'
    libraryVersion = this.version

    // Repository Link (For e.g. GitHub repo)
    siteUrl = 'https://github.com/spoqa/LongPressListener'
    gitUrl = 'https://github.com/spoqa/LongPressListener.git'
    issueTrackerUrl = 'https://github.com/spoqa/LongPressListener/issues'
    // Set githubRepository after project is public
    // githubRepository= 'spoqa/LongPressListener'

    // Developer details
    developerId = 'wisemuji'
    developerName = 'Suhyeon Kim'
    developerEmail = 'dev@spoqa.com'

    // License information
    licenseName = 'The MIT License (MIT)'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

group = publishedGroupId

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                groupId publishedGroupId
                artifactId = artifact
                name libraryName
                description = libraryDescription
                url siteUrl

                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

// Avoid Kotlin docs error
tasks.withType(Javadoc) {
    enabled = false
}

// Remove javadoc related tasks
task javadoc(type: Javadoc) {
    // If using .kt files
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issueTrackerUrl
        // Set githubRepository after project is public
        // githubRepo = githubRepository
        licenses = allLicenses
        userOrg = organization
        publish = true
    }
}
